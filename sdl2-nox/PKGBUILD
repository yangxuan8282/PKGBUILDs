# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=sdl2-nox
pkgver=2.0.8
pkgrel=8
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2)"
arch=('armv7h')
url="http://www.libsdl.org"
license=('MIT')
provides=('sdl2')
replaces=('sdl2')
depends=('glibc')
makedepends=('libxkbcommon' 'cmake')
optdepends=('alsa-lib: ALSA audio driver'
            'libpulse: PulseAudio audio driver')
source=("https://www.libsdl.org/release/SDL2-${pkgver}.tar.gz"{,.sig}
        cmake-include-paths.patch)
sha512sums=('673c6058b8692a36b4a3594456b10ef6051efe79e4fb644421fc5c76b11fd68b895840a2c8b72413418c378733e2993d33f19767d0d7ed101eda6310bd70c869'
            'SKIP'
            '53eb853153b189bd9c0978ab8c7375608b6df5fc78e22168505bf9f6e695f6fbdc553eead0b17deb734002e047c6d5ae06642363fc629aa9ab7bef0c30388c84')
validpgpkeys=('1528635D8053A57F77D1E08630A59377A7763BE6')

prepare() {
  cd SDL2-${pkgver}

  patch -Np1 -i "${srcdir}"/cmake-include-paths.patch

  rm -rf build
  mkdir build
}

build() {
  cd SDL2-${pkgver}/build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DESD=OFF \
      -DVIDEO_OPENGL=OFF \
      -DVIDEO_X11=OFF \
      -DPULSEAUDIO=OFF \
      -DESD=OFF \
      -DVIDEO_MIR=OFF \
      -DVIDEO_WAYLAND=OFF
  make
}

package() {
  cd SDL2-${pkgver}/build

  make DESTDIR="$pkgdir" install

  sed -i "s/libSDL2\.a/libSDL2main.a/g" "$pkgdir"/usr/lib/cmake/SDL2/SDL2Targets-noconfig.cmake
  install -Dm644 ../COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
